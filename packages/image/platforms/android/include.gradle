import groovy.json.JsonSlurper


repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}
dependencies {
	def glideVersion =  project.hasProperty("glideVersion") ? project.glideVersion : "5.0.5"
	def glideCompilerVersion =  project.hasProperty("glideCompilerVersion") ? project.glideCompilerVersion : "4.16.0"
	def glideTransformationsVersion =  project.hasProperty("glideTransformationsVersion") ? project.glideTransformationsVersion : "4.3.0"
	def glideOkHttpVersion =  project.hasProperty("glideOkHttpVersion") ? project.glideOkHttpVersion : "4.11.0"

    def androidXAppCompatVersion = "${ns_default_androidx_appcompat_version}"
    if (project.hasProperty("androidXAppCompat")) {
        androidXAppCompatVersion = androidXAppCompat
        outLogger.withStyle(Style.SuccessHeader).println "\t + using android X library androidx.appcompat:appcompat:$androidXAppCompatVersion"
    }
    implementation "androidx.appcompat:appcompat:$androidXAppCompatVersion"


    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideCompilerVersion"
    implementation "jp.wasabeef:glide-transformations:$glideTransformationsVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideOkHttpVersion"

    if (project.hasProperty("tempBuild")) {
		// we need to copy the code in both places as N main gradle system does not support top level code
        // for now we support only @nativescript/core and @akylas/nativescript
        // we should have a more generic way
		def widgetsDir = "$USER_PROJECT_ROOT/node_modules/@nativescript/core/platforms/android"
        def appPackageJsonFile = file("$USER_PROJECT_ROOT/package.json")
        if (appPackageJsonFile.exists()) {
            def appPackageJson = new JsonSlurper().parseText(appPackageJsonFile.text)
            if (appPackageJson.dependencies['@akylas/nativescript'] != null) {
                widgetsDir = "$USER_PROJECT_ROOT/node_modules/@akylas/nativescript/platforms/android"
            }
        }

        // if ui-mobile-base is provided as source the name is nativescript.aar
	  	//else use widget-release.aar
      	if (file("$widgetsDir/widgets-release.aar").exists()) {
            implementation(name:'widgets-release', ext:'aar')
        } else {
            implementation(name:'nativescript', ext:'aar')
        }
    }
}

repositories {
	mavenCentral()
    // we need to copy the code in both places as N main gradle system does not support top level code
    // for now we support only @nativescript/core and @akylas/nativescript
    // we should have a more generic way
    def widgetsDir = "$USER_PROJECT_ROOT/node_modules/@nativescript/core/platforms/android"
    def appPackageJsonFile = file("$USER_PROJECT_ROOT/package.json")
    if (appPackageJsonFile.exists()) {
        def appPackageJson = new JsonSlurper().parseText(appPackageJsonFile.text)
        if (appPackageJson.dependencies['@akylas/nativescript'] != null) {
            widgetsDir = "$USER_PROJECT_ROOT/node_modules/@akylas/nativescript/platforms/android"
        }
    }
    flatDir {
        dirs "$widgetsDir"
    }
}
